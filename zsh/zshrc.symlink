setopt extended_glob

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# source every .zsh file in this rep except the ones below ./vendor
for config_file ($ZSH/(^vendor*/)#*.zsh) [[ "$(basename $config_file)" == "completion.zsh" ]] || source $config_file

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for config_file ($ZSH/**/completion.zsh) source $config_file

# Initialize vendor plugins

VENDOR=$ZSH/zsh/vendor
source_if_exists () {
    [[ -a "$1" ]] && source "$1"
}

source_if_exists $VENDOR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source_if_exists $VENDOR/zaw/zaw.plugin.zsh
bindkey '^X,' zaw

if [[ -a $VENDOR/zsh-history-substring-search/zsh-history-substring-search.zsh ]]
then
	source $VENDOR/zsh-history-substring-search/zsh-history-substring-search.zsh
	# bind UP and DOWN arrow keys
	bindkey '^[[A' history-substring-search-up
  bindkey '^[[B' history-substring-search-down
fi

# Setup zsh-autosuggestions
if [[ -a $VENDOR/zsh-autosuggestions/autosuggestions.zsh ]]
then
	source $VENDOR//zsh-autosuggestions/autosuggestions.zsh

	# Enable autosuggestions automatically
	zle-line-init() {
		zle autosuggest-start
	}
	zle -N zle-line-init

	# use ctrl+t to toggle autosuggestions(hopefully this wont be needed as
	# zsh-autosuggestions is designed to be unobtrusive)
	bindkey '^T' autosuggest-toggle
fi

# Set Apple Terminal.app resume directory
if [[ $TERM_PROGRAM == "Apple_Terminal" ]] && [[ -z "$INSIDE_EMACS" ]] {
  function chpwd {
    local SEARCH=' '
    local REPLACE='%20'
    local PWD_URL="file://$HOSTNAME${PWD//$SEARCH/$REPLACE}"
    printf '\e]7;%s\a' "$PWD_URL"
  }

  chpwd
}

# Google Cloud SDK.
source "$HOME/SDKs/google-cloud-sdk/path.zsh.inc"
source "$HOME/SDKs/google-cloud-sdk/completion.zsh.inc"

unset VENDOR
